module FreeC.LiftedIR.Converter.Expr
  ( convertExpr
  )
where

import           Control.Monad                  ( foldM )
import           Data.Maybe                     ( fromJust
                                                , fromMaybe
                                                )

import qualified FreeC.IR.Syntax               as IR
import           FreeC.IR.SrcSpan               ( SrcSpan(NoSrcSpan) )
import qualified FreeC.LiftedIR.Syntax         as LIR
import qualified FreeC.LiftedIR.Converter.Type as LIR
import           FreeC.Environment
import           FreeC.Environment.Fresh        ( freshIRQName )
import           FreeC.Monad.Converter

import           Prelude                 hiding ( pure )

convertExpr :: IR.Expr -> Converter LIR.Expr
convertExpr expr = convertExpr' expr [] []

-- | Converts an expression from IR to lifted IR and lifts it during the
--   translation.
--
--   TODO: state translation invariant
convertExpr' :: IR.Expr -> [IR.Type] -> [IR.Expr] -> Converter LIR.Expr

-- | Pass argument from applications to converter for callee, allowing us to
--   convert functions and constructors with full access to their parameters.
--
--   >                $
--   > convertExpr'  / \   [] args = convertExpr' eâ‚ [] (eâ‚‚ : args)
--   >              eâ‚  eâ‚‚
convertExpr' (IR.App _ e1 e2 _) [] args = convertExpr' e1 [] (e2 : args)

-- | Pass type argument from visible type application to converter for callee.
--
--   >                @
--   > convertExpr'  / \   tArgs args = convertExpr' e (Ï„ : tArgs) args
--   >              e   Ï„
convertExpr' (IR.TypeAppExpr _ e t _) typeArgs args =
  convertExpr' e (t : typeArgs) args

convertExpr' (IR.Con srcSpan name _) _ args = do
  args' <- mapM convertExpr args
  let con = LIR.SmartCon srcSpan name undefined
  return $ LIR.App srcSpan con [] [] args' undefined

convertExpr' (IR.Var srcSpan name _) _ args = do
  args'    <- mapM convertExpr args
  function <- inEnv $ isFunction name
  if function
    then do -- top level function (lifted piece wise)
      undefined
    else generateApply (LIR.Var srcSpan name undefined) args'

-- | Integer Literals
convertExpr' (IR.IntLiteral srcSpan value _) [] [] =
  return $ pure srcSpan $ LIR.IntLiteral srcSpan value undefined

-- | Lambda abstractions.
--
-- > â¡     Î“,x:Ï„â‚€ âŠ¢ e:Ï„â‚     â¤'           Î“',x:Ï„â‚€' âŠ¢ e':Ï„â‚'
-- > â¢-----------------------â¥ = -----------------------------------
-- > â£ Î“ âŠ¢ Î»x:Ï„â‚€.e : Ï„â‚€ â†’ Ï„â‚ â¦   Î“' âŠ¢ pure(Î»x:Ï„â‚€'.e') : m(Ï„â‚€' â†’ Ï„â‚')
convertExpr' (IR.Lambda srcSpan args rhs _) [] [] =
  pure srcSpan
    <$> (lambda srcSpan <$> mapM convertVarPat args <*> convertExpr rhs)

-- | @if@-expressions.
--
--   > â¡Î“ âŠ¢ p:Bool  Î“ âŠ¢ t:Ï„  Î“ âŠ¢ f:Ï„â¤'     Î“' âŠ¢ p':Bool'  Î“' âŠ¢ t':Ï„'  Î“' âŠ¢ f':Ï„'
--   > â¢----------------------------â¥ = -------------------------------------------
--   > â£ Î“ âŠ¢ if p then t else f : Ï„ â¦   Î“' âŠ¢ p' >>= Î»x:ğ”¹'.if x then t' else f' : Ï„'
--
-- Note that the argument of the lambda is lifted, but its type is @Bool Shape Pos@,
-- which is just an alias for @bool@, which ignores its arguments.
convertExpr' (IR.If srcSpan cond true false _) [] [] = do
  cond' <- convertExpr cond
  cond' `bind` \d -> ite srcSpan d <$> convertExpr true <*> convertExpr false

-- @case@-expressions.
--
-- > â¡Î“ âŠ¢ e:Ï„â‚€   Î“ âŠ¢ alts:Ï„â‚€ => Ï„â¤'     Î“' âŠ¢ e':Ï„â‚€'     Î“' âŠ¢ alts':Ï„â‚€* => Ï„'
-- > â¢---------------------------â¥ = ------------------------------------------
-- > â£  Î“ âŠ¢ case e of alts : Ï„   â¦   Î“' âŠ¢ e' >>= Î»x:Ï„â‚€*.match x with alts' : Ï„'
--
-- where @alts'@ are the lifted (not smart) constructors for Ï„â‚€.
convertExpr' (IR.Case srcSpan discriminante patterns _) [] [] = do
  discriminant' <- convertExpr discriminante
  discriminant' `bind` \d -> patternMatch srcSpan d <$> mapM convertAlt patterns

-------------------------------------------------------------------------------
-- Lift Patterns                                                             --
-------------------------------------------------------------------------------

convertAlt :: IR.Alt -> Converter LIR.Alt
convertAlt (IR.Alt srcSpan conPat varPats expr) =
  LIR.Alt srcSpan (convertConPat conPat)
    <$> mapM convertVarPat varPats
    <*> convertExpr expr

convertConPat :: IR.ConPat -> LIR.ConPat
convertConPat (IR.ConPat srcSpan name) = LIR.ConPat srcSpan name

convertVarPat :: IR.VarPat -> Converter LIR.VarPat
convertVarPat (IR.VarPat srcSpan ident t _) = do
  ident' <- freshIRQName ident
  return $ LIR.VarPat srcSpan ident' $ LIR.liftType <$> t

-------------------------------------------------------------------------------
-- Application-expression helper                                             --
-------------------------------------------------------------------------------

-- | Applies a n-ary lifted function by applying the rule below repeatedly.
--
--   > â¡Î“ âŠ¢ eâ‚€:Ï„â‚€ â†’ Ï„â‚   Î“ âŠ¢ eâ‚:Ï„â‚€â¤'  Î“' âŠ¢ eâ‚€' : m(Ï„â‚€' â†’ Ï„â‚')    Î“' âŠ¢ eâ‚':Ï„â‚€'
--   > â¢--------------------------â¥ = ---------------------------------------
--   > â£      Î“ âŠ¢ eâ‚€eâ‚ : Ï„â‚       â¦   Î“' âŠ¢ eâ‚€' >>= Î»f:(Ï„â‚€' â†’ Ï„â‚').f eâ‚€' : eâ‚'
generateApply :: LIR.Expr -> [LIR.Expr] -> Converter LIR.Expr
generateApply = foldM $ \mf arg -> mf `bind` \f -> return (f `app` arg)

-------------------------------------------------------------------------------
-- Smart Constructors                                                        --
-------------------------------------------------------------------------------

guessName :: LIR.Expr -> Maybe String
guessName (LIR.Var _ name _  ) = IR.identFromQName name
guessName (LIR.Bind _ arg _ _) = guessName arg
guessName _                    = Nothing

bind :: LIR.Expr -> (LIR.Expr -> Converter LIR.Expr) -> Converter LIR.Expr
bind arg k = do
  argIdent <- freshIRQName $ fromMaybe "f" (guessName arg)
  rhs      <- lambda NoSrcSpan [varPat argIdent] <$> k (var argIdent)
  return $ LIR.Bind NoSrcSpan arg rhs undefined

app :: LIR.Expr -> LIR.Expr -> LIR.Expr
app l@(LIR.App _ _ _ _ _ _) r = l { LIR.exprAppArgs = r : LIR.exprAppArgs l } -- TODO: update types
app l                       r = LIR.App NoSrcSpan l [] [] [r] undefined

var :: LIR.VarName -> LIR.Expr
var ident = LIR.Var NoSrcSpan ident undefined

varPat :: LIR.VarName -> LIR.VarPat
varPat ident = LIR.VarPat NoSrcSpan ident Nothing

lambda :: SrcSpan -> [LIR.VarPat] -> LIR.Expr -> LIR.Expr
lambda srcSpan args rhs = LIR.Lambda srcSpan args rhs $ LIR.funcType
  NoSrcSpan
  (map (fromJust . LIR.varPatType) args)
  (LIR.exprType rhs)

ite :: SrcSpan -> LIR.Expr -> LIR.Expr -> LIR.Expr -> LIR.Expr
ite srcSpan cond true false =
  LIR.If srcSpan cond true false $ LIR.exprType true

pure :: SrcSpan -> LIR.Expr -> LIR.Expr
pure srcSpan expr =
  LIR.Pure srcSpan expr $ LIR.FreeTypeCon srcSpan $ LIR.exprType expr

patternMatch :: SrcSpan -> LIR.Expr -> [LIR.Alt] -> LIR.Expr
patternMatch srcSpan discriminant patterns =
  LIR.Case srcSpan discriminant patterns undefined -- TODO: type


































